// 声明是Android程序，
//com.android.application 表示这是一个应用程序模块
//com.android.library 标识这是一个库模块
//而这区别：前者可以直接运行，后着是依附别的应用程序运行
apply plugin: 'com.android.application'
//文件中第一行使用apply plugin表示应用了一个插件，该插件一般有两种值可选：
//'com.android.application'，表示该模块为应用程序模块，可以直接运行，打包得到的是.apk文件
//'com.android.library'，表示该模块为库模块，只能作为代码库依附于别的应用程序模块来运行，打包得到的是.aar文件

android {
    compileSdkVersion 29//设置编译时用的Android版本
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.rose.test"////项目的包名
        minSdkVersion 23//项目最低兼容的版本
        targetSdkVersion 29//项目的目标版本
        versionCode 1//版本号
        versionName "1.0"//版本名称
        //表明要使用AndroidJUnitRunner进行单元测试
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {//自动化打包配置
        dev { storeFile file('../certs/debug.keystore') }
        integration { storeFile file('../certs/debug.keystore') }
        stage { storeFile file('../certs/debug.keystore') }
        release {
            keyAlias 'key'
            keyPassword '123456'
            storeFile file('../signing_certificates/mytest.jks')
            storePassword '123456'
        }
    }
    buildTypes {//各版本环境配置
        debug {
            signingConfigs.debug
            applicationIdSuffix '.dev'
            testCoverageEnabled false
        }
        integration {
            initWith(debug)
            applicationIdSuffix '.int'
            testCoverageEnabled false

        }
        stage {
            initWith(debug)
            applicationIdSuffix '.stg'
            testCoverageEnabled false
        }
        release {
            minifyEnabled true
            testCoverageEnabled false
        }
    }

    flavorDimensions 'demo'
    productFlavors {
        china {
            applicationId 'com.rose.test.cn'
            dimension 'demo'
        }
        north_america {
            applicationId 'com.rose.test.na'
            dimension 'demo'
        }
        resr_of_the_world {
            applicationId 'com.rose.test'
            dimension 'demo'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
}
